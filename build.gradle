repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://maven.aksw.org/archiva/repository/internal" }
    maven { url "http://maven.aksw.org/archiva/repository/snapshots" }
}

buildscript {
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.1.RELEASE")
    }
    repositories{
        mavenLocal()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
            

}
apply plugin: 'spring-boot'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'distribution'
apply plugin: 'application'


// We use Java 8
sourceCompatibility = 1.8
targetCompatibility = 1.8

//
ext {
    sparqlMapVersion = '0.7.0-SNAPSHOT'
}


// deal with the changing dependency of sparqlmap
configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

configurations{

    compile.exclude module: 'log4j-over-slf4j'
}


springBoot {
    mainClass = "org.aksw.sparqlmap.web.SparqlMapWebApp"
}

jar {
    baseName = 'sparqlmap'
    version =  sparqlMapVersion
}



dependencies {
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    	exclude module: 'spring-boot-starter-logging'
    	exclude module: "logback-classic"

    }
    compile("org.springframework:spring-tx")
    compile("org.springframework.boot:spring-boot-starter-jetty")

    compileOnly "org.projectlombok:lombok:1.16.10"


    compile("org.springframework.boot:spring-boot-starter-actuator"){
    	exclude module: "logback-classic"
    }

    compile group: "org.aksw.sparqlmap", name: "sparqlmap-core", version: sparqlMapVersion, changing: true
    compile 'commons-cli:commons-cli:1.3.1'
    compile "com.zaxxer:HikariCP:2.5.1"
    
    testCompile("junit:junit")
    testCompile 'org.springframework:spring-jdbc:4.1.5.RELEASE'

}



startScripts {
    classpath = files()
    mainClassName = 'org.aksw.sparqlmap.cli.SparqlMapStarter'
    applicationName = 'sparqlmap'
      doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile  = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text.replace('CLASSPATH=', 'CLASSPATH=%APP_HOME%\\lib\\*')
        unixScriptFile.text  = unixScriptFile.text.replace('CLASSPATH=', 'CLASSPATH=$APP_HOME/lib/*')
    }
}

